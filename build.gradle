plugins {
  id 'java'
  id 'org.springframework.boot' version '3.1.2'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.graalvm.buildtools.native' version '0.9.23'
  id 'de.undercouch.download' version '4.1.1'
}

apply plugin: 'java'

group = 'org.springframework.samples'
version = '3.1.2'
sourceCompatibility = '17'

def OPENTELEMETRY_VERSION = '1.32.0'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = uri("https://s01.oss.sonatype.org/content/groups/public/")
  }
}

ext.webjarsFontawesomeVersion = "4.7.0"
ext.webjarsBootstrapVersion = "5.2.3"

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-cache'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'javax.cache:cache-api'
  implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
  runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
  runtimeOnly "org.webjars.npm:bootstrap:${webjarsBootstrapVersion}"
  runtimeOnly "org.webjars.npm:font-awesome:${webjarsFontawesomeVersion}"
  runtimeOnly 'com.github.ben-manes.caffeine:caffeine'
  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'com.mysql:mysql-connector-j'
  runtimeOnly 'org.postgresql:postgresql'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.springframework.boot:spring-boot-docker-compose'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:mysql'

  //Enables instrumentation using @WithSpan
  implementation("io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:${OPENTELEMETRY_VERSION}")
  implementation("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api:${OPENTELEMETRY_VERSION}")

  // used for ClientTester
  implementation("com.squareup.okhttp3:okhttp:4.10.0")
}

tasks.named('test') {
  useJUnitPlatform()
}


// running with agent, will instrument HTTP client calls with library named io.opentelemetry.okhttp-3.0
task runClientTester(type: JavaExec) {
    group = "Execution"
    description = "Run Client Tester"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'petclinic.client.ClientTester'
    jvmArgs("-javaagent:build/otel/opentelemetry-javaagent.jar")
    systemProperty("otel.service.name", "ClientTesterOfPetClinic")
    systemProperty("otel.traces.exporter", "otlp")
    environment("OTEL_RESOURCE_ATTRIBUTES", "digma.environment=DEV")
    systemProperty("otel.exporter.otlp.traces.endpoint", "http://localhost:5050")
    environment("PETSHOP_URL", "http://localhost:9753")
}

bootTestRun {
    dependsOn("downloadOtelAgent")
    dependsOn("downloadDigmaAgentExtension")
}

runClientTester {
    dependsOn("downloadOtelAgent")
    dependsOn("downloadDigmaAgentExtension")
}


// Download the OpenTelemetry java agent and put it in the build directory
task downloadOtelAgent(type: Download) {
    // otel agent version is 1.26.0
    src "https://github.com/open-telemetry/opentelemetry-java-instrumentation/releases/latest/download/opentelemetry-javaagent.jar"
    dest project.buildDir.toString() + "/otel/opentelemetry-javaagent.jar"
    overwrite true
}

// Download the Digma OpenTelemetry agent extension and put it in the build directory
task downloadDigmaAgentExtension(type: Download) {
    // digma extension version is 0.6.14
    src "https://github.com/digma-ai/otel-java-instrumentation/releases/latest/download/digma-otel-agent-extension.jar"
    dest project.buildDir.toString() + "/otel/digma-otel-agent-extension.jar"
    overwrite true
}
